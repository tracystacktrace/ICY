buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://cdn.fox2code.com/maven' }
    }
    dependencies {
        classpath('com.fox2code.FoxLoader:dev:2.0-alpha22')
    }
}

apply plugin: 'foxloader.dev'

group = 'net.tracystacktrace'
version = '1.0.2'

configurations {
    implAndInclude
    implAndInclude.transitive = false
    implementation.extendsFrom(implAndInclude)
}

dependencies {
    api 'org.semver4j:semver4j:5.3.0'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implAndInclude 'net.tracystacktrace:HelloGUI:0.1'
}

test {
    useJUnitPlatform()
}

jar {
    from(provider {
        configurations.implAndInclude.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }) {
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
        exclude '**/package-info.class'
        exclude '**/module-info.class'
        exclude 'assets/*/icon.png'
    }
    from "LICENSE"
}

foxloader {
    modId = "icy"
    modName = "ICY (I See Y[ou])"
    modIcon = "assets/icy/icon.png"
    modVersion = project.version
    modAuthors = "tracystacktrace"
    modMain = "net.tracystacktrace.icy.ICYInit"
    modWebsite = "https://github.com/tracystacktrace/ICY"
    modDesc("\u00A73Provides useful in-game tooltip about aimed blocks")
    modDesc()
    modDesc("\u00A76The mod behavior can be tweaked within the \"\u00A7aConfigure\u00A76\" menu!")
    modDesc()
    modDesc("\u00A76License: \u00A7aApache License 2.0")
    modDesc("\u00A76Sources: \u00A79https://github.com/tracystacktrace/ICY")
}
